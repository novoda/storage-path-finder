def configureStaticAnalysis(Project project) {
    def testSourceSet = project.android.sourceSets.test.java.srcDirs

    apply plugin: 'com.novoda.static-analysis'

    staticAnalysis {
        penalty {
            maxErrors = 0
            maxWarnings = 0
        }

        checkstyle {
            toolVersion '8.5'
            exclude testSourceSet
            configFile teamPropsFile('static-analysis/checkstyle-modules.xml')
        }

        pmd {
            toolVersion '5.8.1'
            exclude testSourceSet
            ruleSetFiles = files(teamPropsFile('static-analysis/pmd-rules.xml'))
            ruleSets = []   // Note: this is a workaround to make the <exclude-pattern>s in pmd-rules.xml actually work
        }

        findbugs {
            toolVersion '3.0.1'
            exclude testSourceSet
            excludeFilter teamPropsFile('static-analysis/findbugs-excludes.xml')
        }
    }
}

project.plugins.withId('com.android.library') {
    configureStaticAnalysis(project)
}

project.plugins.withId('com.android.application') {
    configureStaticAnalysis(project)
}
